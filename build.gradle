apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'jacoco'
apply plugin: 'com.github.kt3k.coveralls'
apply plugin: 'eclipse'
apply plugin: 'com.jfrog.artifactory'

group = 'edu.gcsc.lua'
sourceCompatibility = 1.6
version = '0.2-SNAPSHOT'

if (System.env['TRAVIS'] != null) {
    project.ext.set ("buildInfo.build.number", System.env.TRAVIS_BUILD_NUMBER)
    project.ext.set ("buildInfo.build.name", rootProject.name )
} else {
    project.ext.set ("buildInfo.build.number", '0' )
    project.ext.set ("buildInfo.build.name", rootProject.name+"-local")
}

jar {
    manifest {
        attributes 'Implementation-Title': 'Lua auto-completion library for UG4 shell', 'Implementation-Version': version
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.1'
}

artifacts {
    archives sourcesJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
        }
    }
}

repositories {
    mavenCentral()
    
    maven {
        name 'JFrog OSS snapshot repo'
        url  'https://oss.jfrog.org/oss-snapshot-local/'
    } 
}
      
buildscript {
    repositories {
        mavenCentral()
        maven {
            url 'http://oss.jfrog.org/artifactory/plugins-release'    
        }
    }
    
    dependencies {
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.0.1'
        classpath 'org.jfrog.buildinfo:build-info-extractor-gradle:3.0.1'
    }
}        

dependencies {
    compile group: 'commons-collections', name: 'commons-collections', version: '3.2'
    testCompile group: 'junit', name: 'junit', version: '4.10'

    compile group: 'edu.gcsc.lua', name: 'luaautocomplete', version: '0.2-SNAPSHOT'
}

jacocoTestReport {
    reports {
        xml.enabled = true // coveralls plugin depends on xml format report
        html.enabled = true
    }
}

artifactory {
    contextUrl = "http://oss.jfrog.org/artifactory"   //The base Artifactory URL if not overridden by the publisher/resolver
    publish {
        repository {
            repoKey = 'oss-snapshot-local'
            username = "turpid-monkey"
            password = System.getenv()['bintrayKey']
            maven = true
        }
        defaults {
            publications ('mavenJava')
        }
    }
    resolve {
        repository {
            repoKey = 'libs-release'
            maven = true
             
        }
    }
}
